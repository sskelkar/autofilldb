plugins { id "io.spring.dependency-management" version "1.0.5.RELEASE" }

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

ext { springBootVersion = '2.1.2.RELEASE' }

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

signing {
	sign configurations.archives
}

group = "com.github.sskelkar.autofilldb"
archivesBaseName = "autofilldb"
version = '0.1'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencyManagement {
	imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'com.github.sskelkar.autofilldb'
				packaging 'jar'
				// optionally artifactId can be defined here
				description 'autofilldb is a lightweight library that can automatically populate fake data in database, while satisfying column constraints.'
				url 'https://github.com/sskelkar/autofilldb'

				scm {
					connection 'scm:git:https://github.com/sskelkar/autofilldb.git'
					developerConnection 'scm:git:git@github.com:sskelkar/autofilldb.git'
					url 'https://github.com/sskelkar/autofilldb.git'
				}

				licenses {
					license {
						name 'MIT License'
						url 'https://github.com/sskelkar/autofilldb/blob/master/license'
					}
				}

				developers {
					developer {
						id 'sskelkar'
						name 'Sojjwal Kelkar'
						email 'sojjwalkelkar@gmail.com'
					}

					developer {
						id 'shreyasm'
						name 'Shreyas Mahajan'
						email 'mahajan.shreyas@gmail.com'
					}
				}
			}
		}
	}
}